//plugins {
//    id 'com.android.application'
//}

if (isRelease) {
    apply plugin: 'com.android.library' //->打成arr
} else {
    apply plugin: 'com.android.application' //非发布模式，apk打包
}

android {
    compileSdkVersion app_android.compileSdkVersion
    buildToolsVersion app_android.buildToolsVersion

    defaultConfig {
        //组件化模式下才需要applicationId
        if (!isRelease) {
            applicationId "com.example.personal"
        }
        minSdkVersion app_android.minSdkVersion
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //把包名传递进去
        javaCompileOptions {
            annotationProcessorOptions{
                arguments = [moduleName: this.project.getName(), packageNameForAPT: packageNameForAPT]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // 源集 —— 用来设置Java目录或者资源目录
    sourceSets {
        main {
            if(!isRelease){
                // 如果是组件化模式，需要单独运行时
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            }else {
                // 集成化模式，整个项目打包
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    // release 时 debug 目录下文件不需要合并到主工程
                    //exclude '**/debug/**'
                }
            }
        }
    }
}

dependencies {
    //导入common模块
    implementation project(':common')
    implementation project(':annotation')
    annotationProcessor project(':annotation_processor')

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}